// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t]==1) out[t+1] = 0
 * else if (load[t]==1)  out[t+1] = in[t]
 * else if (inc[t]==1)   out[t+1] = out[t] + 1  (integer addition)
 * else                  out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
      Or(a = load, b = inc, out = t1);
      Or(a = reset, b = t1, out = regLoad);

      Mux8Way16(a = regOUT, b = incOUT, c = in, d = in, 
                e = false, f = false, g = false, h = false, 
                sel[0] = inc, sel[1] = load, sel[2] = reset, out = muxOUT);
      Register(in = muxOUT, load = regLoad, out = regOUT, out = out);
      Inc16(in = regOUT, out = incOUT);
}
